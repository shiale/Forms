
&НаКлиенте
Процедура СписокГруппВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоРодителю();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоРодителю()
	
	ЭлементыОтбора = Список.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ОтборПоРодителю = Неопределено;
	ПолеРодитель = Новый ПолеКомпоновкиДанных("Родитель");
	
	Для каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") И
			ЭлементОтбора.ЛевоеЗначение = ПолеРодитель Тогда
			ОтборПоРодителю = ЭлементОтбора;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтборПоРодителю = Неопределено Тогда
		ОтборПоРодителю = ЭлементыОтбора.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборПоРодителю.ЛевоеЗначение = ПолеРодитель; 
	КонецЕсли;
	
	ОтборПоРодителю.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборПоРодителю.ПравоеЗначение = Элементы.СписокГрупп.ТекущаяСтрока;
	ОтборПоРодителю.Использование = Истина;
	ОтборПоРодителю.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
КонецПроцедуры // УстановитьОтборПоРодителю()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтборПоРодителю();
КонецПроцедуры

&НаКлиенте
Процедура СписокГруппПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ПриАктивизацииСтрокиЗавершение",0.2,Истина);
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииСтрокиЗавершение()
	УстановитьОтборПоРодителю();
КонецПроцедуры // ПриАктивизацииСтрокиЗавершение()


